# Badhron configuration file
#
# This will define the following variables:
# - Badhron_DIR            // path to this configuration file
# - Badhron_IMPALA_FILES   // source files to be included in your project's anydsl_runtime_wrap call
# - Badhron_LIBRARIES      // Badhron libraries to be included in your project's target_link_libraries call

cmake_minimum_required(VERSION 3.12...3.14)

find_path(Badhron_DIR
	NAMES
		badhron-config.cmake
	PATHS
		${Badhron_DIR}
		$ENV{Badhron_DIR}
		${CMAKE_CURRENT_LIST_DIR}
		"@CMAKE_CURRENT_BINARY_DIR@"
		"@CMAKE_INSTALL_PREFIX@"
)

find_path(Badhron_ROOT_DIR
	NAMES
		cmake/badhron-config.cmake.in
		src/impala/groups.impala
		src/impala/results.impala
	PATHS
		${Badhron_ROOT_DIR}
		$ENV{Badhron_ROOT_DIR}
		"@CMAKE_CURRENT_SOURCE_DIR@"
		"@CMAKE_SOURCE_DIR@"
)

set(Badhron_IMPALA_FILES
	${Badhron_ROOT_DIR}/src/impala/utils.impala
	${Badhron_ROOT_DIR}/src/impala/reports.impala
	${Badhron_ROOT_DIR}/src/impala/results.impala
	${Badhron_ROOT_DIR}/src/impala/checks.impala
	${Badhron_ROOT_DIR}/src/impala/groups.impala
	${Badhron_ROOT_DIR}/src/impala/badhron.impala
)

if(NOT TARGET badhron)
	include(${Badhron_DIR}/badhron-exports.cmake)
endif()
set(Badhron_LIBRARIES badhron;)

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(Badhron DEFAULT_MSG Badhron_DIR Badhron_IMPALA_FILES Badhron_LIBRARIES)

mark_as_advanced(Badhron_ROOT_DIR Badhron_IMPALA_FILES Badhron_LIBRARIES)
