// ====================================================================================================================
// check result
struct BadhronCheckResult {
	passed:  i32,
	failed:  i32,
	pending: i32
}

fn badhron_new_check_result() -> BadhronCheckResult {
	BadhronCheckResult {
		passed:  0,
		failed:  0,
		pending: 0
	}
}


// ====================================================================================================================
// suite result
struct BadhronSuiteResult {
	total_result: BadhronCheckResult,
	group_result: BadhronCheckResult,
	split_result: BadhronCheckResult,
	split_name:   &[u8],
	in_split:     bool,
	split_count:  i32,
	exit_status:  i32
}

fn badhron_new_suite_result() -> BadhronSuiteResult {
	BadhronSuiteResult{
		total_result: badhron_new_check_result(),
		group_result: badhron_new_check_result(),
		split_result: badhron_new_check_result(),
		split_name:   0 as &[u8],
		in_split:     false,
		split_count:  0,
		exit_status:  0
	}
}


// ====================================================================================================================
// result handler
struct BadhronResultHandler {
	// check results
	passed:        fn() -> (),
	failed:        fn() -> (),
	pending :      fn() -> (),

	// reports
	print_reports: fn() -> (),

	// splits
	begin_split:   fn(&[u8]) -> (),
	end_split:     fn() -> BadhronCheckResult,
	split_name:    fn() -> &[u8],
	split_count:   fn() -> i32,

	// group
	group_result:  fn() -> BadhronCheckResult,

	// total result
	reset:         fn() -> (),
	total_result:  fn() -> BadhronCheckResult,

	// misc
	id:            fn() -> u64,
	exit_status:   fn() -> i32
}

fn badhron_new_result_handler(handler_id: u64) -> BadhronResultHandler {
	let mut suite = badhron_new_suite_result();
	BadhronResultHandler {
		// check results
		passed:        || {
			if suite.in_split {
				suite.split_result.passed++;
			} else {
				suite.group_result.passed++;
			}
		},

		failed:        || {
			if suite.in_split {
				suite.split_result.failed++;
			} else {
				suite.group_result.failed++;
			}
			suite.exit_status = 1;
		},

		pending:       || {
			if suite.in_split {
				suite.split_result.pending++;
			} else {
				suite.group_result.pending++;
			}
		},

		// reports
		print_reports: || { badhron_print_reports(handler_id); },

		// splits
		begin_split:   |name| {
			suite.split_name = name;
			suite.in_split = true;
			suite.split_result.passed   = 0;
			suite.split_result.failed   = 0;
			suite.split_result.pending  = 0;
			suite.split_count++;
		},

		end_split:     || {
			suite.split_name = 0 as &[u8];
			suite.in_split = false;
			suite.total_result.passed  += suite.split_result.passed;
			suite.total_result.failed  += suite.split_result.failed;
			suite.total_result.pending += suite.split_result.pending;
			suite.split_result
		},

		split_name:    || { suite.split_name },

		split_count:   || { suite.split_count },

		// group
		group_result:  || { suite.group_result },

		// total result
		reset:         || {
			suite.total_result.passed  = 0;
			suite.total_result.failed  = 0;
			suite.total_result.pending = 0;
			suite.group_result.passed  = 0;
			suite.group_result.failed  = 0;
			suite.group_result.pending = 0;
			suite.split_result.passed  = 0;
			suite.split_result.failed  = 0;
			suite.split_result.pending = 0;
			suite.split_count          = 0;
			suite.in_split             = false;
		},

		total_result:  || {
			suite.total_result.passed  += suite.group_result.passed;
			suite.total_result.failed  += suite.group_result.failed;
			suite.total_result.pending += suite.group_result.pending;
			suite.total_result
		},

		id:            || { handler_id },

		exit_status:   || { suite.exit_status }
	}
}

extern "C" {
	fn badhron_create_result_handler() -> u64;
	fn badhron_delete_result_handler(u64) -> ();

	fn badhron_print_reports(u64) -> ();
}
