// ====================================================================================================================
// check result
struct BadhronCheckResult {
	passed:  i32,
	failed:  i32,
	pending: i32
}

fn badhron_new_check_result() -> BadhronCheckResult {
	BadhronCheckResult {
		passed:  0,
		failed:  0,
		pending: 0
	}
}


// ====================================================================================================================
// suite result
struct BadhronSuiteResult {
	total_result: BadhronCheckResult,
	group_result: BadhronCheckResult,
	split_result: BadhronCheckResult,
	split_name:   &[u8],
	in_split:     bool,
	exit_status:  i32
}

fn badhron_new_suite_result() -> BadhronSuiteResult {
	BadhronSuiteResult{
		total_result: badhron_new_check_result(),
		group_result: badhron_new_check_result(),
		split_result: badhron_new_check_result(),
		split_name:   0 as &[u8],
		in_split:     false,
		exit_status:  0
	}
}


// ====================================================================================================================
// result handler
struct BadhronResultHandler {
	// check results
	passed:        fn() -> (),
	failed:        fn() -> (),
	pending :      fn() -> (),

	// splits
	begin_split:   fn(&[u8]) -> (),
	end_split:     fn() -> BadhronCheckResult,
	split_name:    fn() -> &[u8],

	// group
	begin_group:   fn(&[u8]) -> (),
	end_group:     fn() -> BadhronCheckResult,

	// total result
	print:         fn() -> (),
	total_result:  fn() -> BadhronCheckResult, // TODO remove

	// misc
	id:            fn() -> u64,
	exit_status:   fn() -> i32
}

fn badhron_new_result_handler(handler_id: u64) -> BadhronResultHandler {
	let mut suite = badhron_new_suite_result();
	BadhronResultHandler {
		// check results
		passed:        || {
			if suite.in_split {
				suite.split_result.passed++;
			} else {
				suite.group_result.passed++;
			}
		},

		failed:        || {
			if suite.in_split {
				suite.split_result.failed++;
			} else {
				suite.group_result.failed++;
			}
			suite.exit_status = 1;
		},

		pending:       || {
			if suite.in_split {
				suite.split_result.pending++;
			} else {
				suite.group_result.pending++;
			}
		},

		// splits
		begin_split:   |name| {
			badhron_add_subgroup(handler_id, name);
			suite.split_name = name;
			suite.in_split = true;
			suite.split_result.passed   = 0;
			suite.split_result.failed   = 0;
			suite.split_result.pending  = 0;
		},

		end_split:     || {
			suite.split_name = 0 as &[u8];
			suite.in_split = false;
			suite.total_result.passed  += suite.split_result.passed;
			suite.total_result.failed  += suite.split_result.failed;
			suite.total_result.pending += suite.split_result.pending;
			badhron_set_subgroup_result(
				handler_id,
				suite.split_result.passed,
				suite.split_result.failed,
				suite.split_result.pending
			);
			suite.split_result
		},

		split_name:    || { suite.split_name },

		// group
		begin_group:   |name| {
			badhron_add_group(handler_id, name);
			suite.total_result.passed  = 0;
			suite.total_result.failed  = 0;
			suite.total_result.pending = 0;
			suite.group_result.passed  = 0;
			suite.group_result.failed  = 0;
			suite.group_result.pending = 0;
			suite.split_result.passed  = 0;
			suite.split_result.failed  = 0;
			suite.split_result.pending = 0;
			suite.in_split             = false;
		},

		end_group:     || {
			badhron_set_group_result(
				handler_id,
				suite.group_result.passed,
				suite.group_result.failed,
				suite.group_result.pending
			);
			suite.group_result
		},

		// total result
		print:         || { badhron_print_suite(handler_id); },

		total_result:  || {
			suite.total_result.passed  += suite.group_result.passed;
			suite.total_result.failed  += suite.group_result.failed;
			suite.total_result.pending += suite.group_result.pending;
			suite.total_result
		},

		id:            || { handler_id },

		exit_status:   || { suite.exit_status }
	}
}

extern "C" {
	fn badhron_create_suite() -> u64;
	fn badhron_delete_suite(u64) -> ();

	fn badhron_add_group(u64, &[u8]) -> ();
	fn badhron_add_subgroup(u64, &[u8]) -> ();

	fn badhron_set_group_result(u64, i32, i32, i32) -> ();
	fn badhron_set_subgroup_result(u64, i32, i32, i32) -> ();

	fn badhron_print_suite(u64) -> ();
}
